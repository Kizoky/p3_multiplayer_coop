behavior
{
	name bh_player
	//inherited default_behavior
	states
	{
		st_player
		{
			group Alert
			patterns
			{
				pt_default
				{
					actions 
					{
						SetAttr "wpn_HaveGamamet 1"
						SetAttr "ea_status 0,0,10"
						SetAttr "old_status 0,0,10"
						SetAttr "HealthGain 3"
						SetAttr "WeaponConfirmed 1"
						SetAttr "active 1"
						SetAttr "NumFF 0,0,FRIENDLY_FIRE_MAX"
						Timer 	"tRegenerate,0.06,repeated"
						Timer	"tStopRegen,1"
						Timer 	"tKiller,0"
						Timer	tFireWeapon,2,repeated
						SetAttr "Brutality 0,0,BRUTALITY_MAX"
						Timer	tDecreaseBrutality,BRUTALITY_DECR_TIMER,repeated
						Timer	tPrimaryAttack,3
						Timer	tSecondaryAttack,3
                        Timer   tAnchorInfoUpdate,1,repeated
                        Timer   tAnchorUpdate,10,repeated
						Pattern .pt_update
					}
				}
				pt_sucked
				{
					actions
					{
						EntFireUser gri_logic,3
						EntFireUser pw_logic,3
					}
				}
				pt_update
				{
					actions
					{
					}
					events
					{
						OnAttrChange_ea_status 		"ExecutePattern .xpt_OnStatusChange"
						OnAttrChange_ea_armed		"ExecutePattern .xpt_WeaponChange"
						OnTimer_tRegenerate 		"ExecutePattern .xpt_regenerate"
						OnTimer_tWeaponChanged		"ExecutePattern .xpt_WeaponChangeConfirm"
						//OnTimer_tAEFireWeapon		"ExecutePattern .xpt_tAEFireWeapon"
						OnSucked					"ExecutePattern st_player.pt_sucked"
						OnCageOpen 					"ExecutePattern .xpt_OpenCage"
						OnHit						"ExecutePattern .xpt_StopRegen"
						OnDamaged					"ExecutePattern .xpt_StopRegen"
						OnFire_Hit					"ExecutePattern .xpt_StopRegen"
						OnTimer_tStopRegen			"SetAttr CanRegen 1"
						OnPrimaryAttack				"ExecutePattern .xpt_OnPrimaryAttack"
						OnSecondaryAttack			"ExecutePattern .xpt_OnSecondaryAttack"
						OnTimer_tKiller				"ExecutePattern .xpt_ReleaseStatus"
						OnFire_HostagedMe			"ExecutePattern .xpt_Hostaged"
						OnFire_ResquedMe			"ExecutePattern .xpt_Released"
						OnTimer_tFireWeapon			"ExecutePattern .xpt_FireWeapon"
						OnAcid						"ExecutePattern .xpt_OnAcid"
						OnFire_KickedMe				"ExecutePattern .xpt_OnWrongAct"
						OnFire_HitedMe				"ExecutePattern .xpt_OnHitedSomebody"
						OnFire_KilledMe				"ExecutePattern .xpt_OnKilledSomebody"
						OnFire_DisabledMe			"ExecutePattern .xpt_OnBrutalAct"
						OnTimer_tDecreaseBrutality	"IfAttr Brutality != BRUTALITY_MAX ChangeAttr Brutality -1"
						OnFire_Brutality			"ExecutePattern .xpt_OnBrutalitySeen"
						OnFire_FriendlyFire			"ExecutePattern .xpt_OnFriendlyFire"
						OnTimer_tDecreaseFF			"ChangeAttr NumFF -1"
                        OnTimer_tAnchorInfoUpdate   "ExecutePattern .xpt_AnchorInfoUpdate"
                        OnTimer_tAnchorUpdate       "ExecutePattern .xpt_AnchorUpdate"
						
						OnSegwayEnter				"Wait 0"
						OnSegwayLeave				"Wait 0"
						
						OnDeath						"ExecutePattern st_OnDeath.pt_default"
						//OnFire_JesusFailed			"ExecutePattern .xpt_OnJesusFailed"
						//OnFinaleWin					"ExecutePattern .xpt_OnEndGame"
					}
				}
				xpt_StopRegen
				{
					actions
					{
						RemoveAttr CanRegen
						//SetAttr "HealthGain 1"
						Timer tStopRegen,10
					}
				}
				xpt_regenerate
				{
					actions
					{
						CheckAttr "CanRegen Block begin"
							IfAttr "ea_health < 100 Block begin"
								SetAttr "NewHealth ea_health"
								ChangeAttr "NewHealth +HealthGain"
								
								IfAttr "NewHealth >= 100 SetAttr ea_health 100"
								IfAttr "NewHealth <  100 ChangeAttr ea_health +HealthGain"
								//ChangeAttr "HealthGain +1"
							Block end
						Block end
						//IfAttr "70 > ea_health and ea_health >= 50 ChangeAttr ea_health +1"
						//IfAttr "50 > ea_health and ea_health >= 25 ChangeAttr ea_health +2"
						//IfAttr "ea_health < 25 ChangeAttr ea_health +5"
					}
				}
				xpt_WeaponChange
				{
					actions
					{
						SetAttr "WeaponConfirmed 0"
						Timer tWeaponChanged,2.0
					}
				}
				xpt_WeaponChangeConfirm
				{
					actions
					{
						SetAttr "WeaponConfirmed 1"
						
						IfAttr "ea_ActiveWeapon >= 5 Block begin"
							IfAttr "ea_ActiveWeapon == 5 SetAttr ea_status sPUNISHER"
							IfAttr "ea_ActiveWeapon >= 6 SetAttr ea_status sMILITANT"
							ExecutePattern .xpt_FireWeapon
							SetAttr "HaveScaryWeapon 1"
						Block end
						IfAttr "ea_ActiveWeapon < 5 Block begin"
							SetAttr "ea_status sNEUTRAL"
							RemoveAttr HaveScaryWeapon
						Block end
						IfAttr "ea_armed == WPN_WEEMOTE SetAttr ea_status sGROSS"
					}
				}
				xpt_FireWeapon
				{
					actions
					{
						IfAttr "WeaponConfirmed != 1 Return 1" //оружие не подтверждено
						IfAttr "ea_ActiveWeapon <  5 Return 1" //нестрашное оружие
						IfAttr "ea_ActiveWeapon == 5 and ea_status != sKILLER Return 1"
						//страшное милишное оружие, но никого не бил
						
						//если кого-то бил страшным милишным оружием
						//или просто ходит с огнестрелом, то файрим эвент
						CheckAttr "HaveScaryWeapon AreaEvent HasWeapon,12"
					}
				}
				xpt_OnPrimaryAttack
				{
					actions
					{
						IfAttr "Timer:tPrimaryAttack > 0 Return 1"
						Timer tPrimaryAttack,3
						//IfAttr "ea_ActiveWeapon >= 5 Block begin"
							//ExecutePattern .xpt_SaveStatus
							//SetAttr "ea_status sKILLER"
							//Timer tKiller,20
						//Block end
						
						//Brutal primary attacks
						IfAttr "ea_armed == WPN_GRENADE		ExecutePattern .xpt_OnBrutalAct"
						IfAttr "ea_armed == WPN_MOLOTOV		ExecutePattern .xpt_OnBrutalAct"
						IfAttr "ea_armed == WPN_BEES		ExecutePattern .xpt_OnBrutalAct"
						
						//Wrong primary attacks
						IfAttr "ea_armed == WPN_GAS			ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_SPRAY		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_CATS		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_CATNIP		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_TASER		ExecutePattern .xpt_OnWrongAct"
					}
				}
				xpt_OnSecondaryAttack
				{
					actions
					{
						IfAttr "Timer:tSecondaryAttack > 0 Return 1"
						Timer tSecondaryAttack,3
						//Brutal secondary attacks
						IfAttr "ea_armed == WPN_BEES		ExecutePattern .xpt_OnBrutalAct"
						IfAttr "ea_armed == WPN_SPRAY		ExecutePattern .xpt_OnBrutalAct"
						
						//Wrong secondary attacks
						IfAttr "ea_armed == WPN_GAS			ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_GRENADE		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_MOLOTOV		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_SHOPVAC		ExecutePattern .xpt_OnWrongAct"
						IfAttr "ea_armed == WPN_TASER		ExecutePattern .xpt_OnWrongAct"
					}
				}
				xpt_OnWrongAct
				{
					actions
					{
						AreaEvent WrongAct,16
					}
				}
				xpt_OnBrutalAct
				{
					actions
					{
						AreaEvent BrutalAct,16
					}
				}
				xpt_OnBrutalitySeen
				{
					actions
					{
						IfAttr "Timer:tBrutal > 0 Return 1"
						ChangeAttr "Brutality +1"
						Timer	tDecreaseBrutality,BRUTALITY_DECR_TIMER,repeated
						Timer	tBrutal,2
					}
				}
				xpt_OnStatusChange
				{
					actions
					{
						IfAttr "Timer:tKiller > 0 and ea_status != sKILLER Block begin"
							SetAttr "old_status ea_status"
							SetAttr "ea_status sKILLER"
						Block end
					}
				}
				xpt_OnHitedSomebody
				{
					actions
					{
						IfAttr "ea_armed == WPN_NONE ExecutePattern .xpt_OnBrutalAct"
						IfAttr "ea_ActiveWeapon >= 5 ExecutePattern .xpt_BecomeKiller"
					}
				}
				xpt_OnKilledSomebody
				{
					actions
					{
						ExecutePattern .xpt_BecomeKiller
						CheckAttr "caller.flag_bystander Block begin"
							IfAttr "DistTo:caller < ED_FAR AreaEvent PlayerKillBystander,16"
						Block end
						
						IfAttr "caller.ea_manner == Manner:JusticeMan and DistTo:caller < ED_FAR AreaEvent PlayerKillPoliceman,16"
					}
				}
				xpt_BecomeKiller
				{
					actions
					{
						ExecutePattern .xpt_SaveStatus
						SetAttr "ea_status sKILLER"
						Timer tKiller,30
					}
				}
				xpt_SaveStatus
				{
					actions
					{
						IfAttr "ea_status != sKILLER SetAttr old_status ea_status"
					}
				}
				xpt_ReleaseStatus
				{	
					actions
					{
						SetAttr "ea_status old_status"
						SetAttr "old_status sNEUTRAL"
					}
				}
				xpt_OnFriendlyFire
				{
					actions
					{
						IfAttr "Timer:tBlockFF > 0 Return 1"
						
						ChangeAttr "NumFF +1"
						Timer tBlockFF,FRIENDLY_FIRE_BLOCK_TIMER
						Timer tDecreaseFF,FRIENDLY_FIRE_DECREASE_TIMER,repeated
					}
				}
				xpt_Hostaged
				{
					actions
					{
						TargetCaller 1
						TargetToMem msTEMP
						SetAttr "HaveHostage 1"
					}
				}
				xpt_Released
				{
					actions
					{
						ClearMem msTEMP
						ResetTarget 1
						RemoveAttr "HaveHostage"
					}
				}
				xpt_OnAcid
				{
					actions
					{
						ExecutePattern .xpt_StopRegen
						// Kizoky: this is handled in Source code instead, please look at p3_registry
						//ChangeAttr "ea_health -30" 
					}
				}
				xpt_OpenCage
				{
					actions
					{
						SetAttr "rnd 0"
						ChangeAttr "rnd 0:10"
						IfAttr "rnd > 3 EntFireInput proxy,Say:MLIFREE"
					}
				}
                
                xpt_AnchorInfoUpdate
                {
                	actions
					{
						Anchor true
					}
                }
                
                xpt_AnchorUpdate
                {
                	actions
					{
                        TargetToMem 7
                        TargetAnchor 1
                        SetAttr "distToAnchor DistTo:target,0,10000"
						TargetFromMem 7
                        ClearMem 7
					}
                }
				
				//игрок кого-то убил и запорол ачивку Personal Jesus
				xpt_OnJesusFailed
				{
					actions
					{
						CheckAttr "flag_Jesus Block begin"
							CheckAttr "debug_ShowJesusMsg ShowMessage [Jesus failed],$P3Mission"
							RemoveAttr flag_Jesus
							Return 1
						Block end
						//ShowMessage "[Jesus failed again],$P3Mission"
					}
				}
				
				xpt_OnEndGame
				{
					actions
					{
						TargetEntByName endgame_controller
						FireEvent EndGame
					}
				}
			}
		}
		
		st_OnDeath
		{
			Group Alert
			Patterns
			{
				pt_default
				{
					actions
					{
						RemoveAttr CanRegen
						State st_corpse
					}
				}
			}
		}
		
		st_corpse
		{
			Group Alert
			Patterns
			{
				pt_default
				{
					actions
					{
						Timer tFireEvent,3,repeated
						SetAttr "ea_status sCORPSE"
						RemoveAttr active
						Wait 1
						AreaEvent PlayerDead,8
					}
					events
					{
						OnTimer_tFireEvent "AreaEvent PlayerDead,8"
					}
				}
			}
		}
		
	}
	
}
